subroutine rhom_to_Hex(nvib, natom, aa, aa_h, x, x_h, v, v_h, sym, sym_h)
	use kinds, only: DP
	implicit none
	!----------------------------------------------------------------------------
	integer           :: natom, nvib
	integer           :: na3
	real(DP)          :: aa   (3,3)
	real(DP)          :: aa_h (3,3)
	real(DP)          :: x    (3,natom          )
	real(DP)          :: x_h  (3,natom * 3      )
	real(DP)          :: v    (3,natom    , nvib)
	real(DP)          :: v_h  (3,natom * 3, nvib)
	character(*)      :: sym  (  natom          )
	character(*)      :: sym_h(  natom * 3      )
	!----------------------------------------------------------------------------
	integer           :: nG(3)
	integer           :: ia, ib, ic, n, i, j, k
	real(DP)          :: x1(3), G_vec(3)
	logical, external :: in_cry_cell
	!----------------------------------------------------------------------------
	na3 = natom * 3
	!----------------------------------------------------------------------------
	aa_h(:,1) = aa(:,1) - aa(:,2)
	aa_h(:,2) = aa(:,2) - aa(:,3)
	aa_h(:,3) = aa(:,1) + aa(:,2) + aa(:,3)
	!----------------------------------------------------------------------------
	n     = 4
	k     = 0
	nG(1) = 0
	nG(2) = 0
	nG(3) = 0
	!----------------------------------------------------------------------------
	do ia = -n, n
		do ib = -n, n
			do ic = -n, n
			!----------------------------------------------------------------------
				do i=1, natom
					!----------------------------------------------------------------
					G_vec = ia * aa(:,1) + ib * aa(:,2) + ic * aa(:,3)
					x1 = x(:,i) + G_vec
					!----------------------------------------------------------------
					call coord_cart_to_crys(1, aa_h, x1)
					!----------------------------------------------------------------
					if(in_cry_cell(nG,x1)) then
						call coord_crys_to_cart(1, aa_h, x1)
						k        = k + 1
						x_h(:,k) = x1
						sym_h(k) = sym(i)
						do j=1, nvib
							v_h(:,k,j) = v(:,i,j)
						end do
					end if
				end do
			!----------------------------------------------------------------------
			end do
		end do
	end do
	!----------------------------------------------------------------------------
	return
end subroutine
