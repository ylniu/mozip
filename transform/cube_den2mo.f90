program cube_den2mo
	use kinds, only: DP
	implicit none
	!------------------------------------------------------------------------------
	integer               :: fid, i, ix, iy, iz
	integer               :: natom, n(3)
	real(DP)              :: a(3,3)
	real(DP)              :: x0(3)
	integer , allocatable :: nat(:)
	real(DP), allocatable :: charge(:)
	real(DP), allocatable :: x(:,:)
	real(DP), allocatable :: grid(:,:,:)
	character(200)        :: fname, line1, line2
	!------------------------------------------------------------------------------
	fid = 1
	call getarg(1, fname)
	!------------------------------------------------------------------------------
	open(fid, file=fname, status="old")
		!---------------------------------------------------------------------------
		read(fid, '(a)') line1
		read(fid, '(a)') line2
		!---------------------------------------------------------------------------
		read(fid,*) natom, x0
		do i=1, 3
			read(fid,*) n(i), a(:,i)
		end do
		!---------------------------------------------------------------------------
		natom=abs(natom)
		!---------------------------------------------------------------------------
		allocate(nat   (  natom         ))
		allocate(charge(  natom         ))
		allocate(x     (3,natom         ))
		allocate(grid  (n(1), n(2), n(3)))
		!---------------------------------------------------------------------------
		do i=1, natom
			read(fid,*) nat(i), charge(i), x(:,i)
		end do
		!---------------------------------------------------------------------------
		do ix=1, n(1)
			do iy=1, n(2)
				read(fid,*) (grid(ix,iy,iz), iz=1, n(3))
			end do
		end do
		!---------------------------------------------------------------------------
	close(fid)
	!------------------------------------------------------------------------------
	open(fid, file=fname)
		write(fid, '(x,a)') trim(adjustl(line1))
		write(fid, '(x,a)') trim(adjustl(line2))
		!---------------------------------------------------------------------------
		write(fid, '(i5,3f12.6,i5)') -natom, x0, 1
		!---------------------------------------------------------------------------
		do i=1, 3
			write(fid, '(i5, 3f12.6)') n(i), a(:,i)
		end do
		!---------------------------------------------------------------------------
		do i=1, natom
			write(fid, '(i5, 4f12.6)') nat(i), charge(i), x(:,i)
		end do
		write(fid, '(2i5)') 1, 1
		!---------------------------------------------------------------------------
		do ix=1, n(1)
			do iy=1, n(2)
				write(fid,'(6e13.5)') (grid(ix,iy,iz), iz=1, n(3))
			end do
		end do
		write(fid,*)
		!---------------------------------------------------------------------------
	close(fid)
	!------------------------------------------------------------------------------
	deallocate(nat   )
	deallocate(charge)
	deallocate(x     )
	deallocate(grid  )
	!------------------------------------------------------------------------------
	stop
end
